//  Подсистема "Модель текста"
//	Автор: Калякин Андрей Г.
//  https://github.com/KalyakinAG/kassl

Перем Строки Экспорт;
Перем СтрокаОтступа Экспорт;
Перем Уровень Экспорт;

Функция Добавить(Текст, П1 = Неопределено, П2 = Неопределено, П3 = Неопределено, П4 = Неопределено, П5 = Неопределено, П6 = Неопределено, П7 = Неопределено, П8 = Неопределено, П9 = Неопределено) Экспорт
	СтрокиШаблона = СтрРазделить(Текст, Символы.ПС);
	Для Каждого СтрокаШаблона Из СтрокиШаблона Цикл
		Строка = СтрокаОтступа + Общий.Шаблон(СтрокаШаблона, П1, П2, П3, П4, П5, П6, П7, П8, П9);
		Строки.Добавить(Строка);
	КонецЦикла;
	Возврат ЭтотОбъект;
КонецФункции

Функция Присоединить(Текст, П1 = Неопределено, П2 = Неопределено, П3 = Неопределено, П4 = Неопределено, П5 = Неопределено, П6 = Неопределено, П7 = Неопределено, П8 = Неопределено, П9 = Неопределено) Экспорт
	СтрокиШаблона = СтрРазделить(Текст, Символы.ПС);
	ЭтоПерваяСтрока = Истина;
	Для Каждого СтрокаШаблона Из СтрокиШаблона Цикл
		Если ЭтоПерваяСтрока = Истина Тогда
			ИндексТекущейСтроки = Строки.ВГраница();
			Строки[ИндексТекущейСтроки] = Строки[ИндексТекущейСтроки] + Общий.Шаблон(СтрокаШаблона, П1, П2, П3, П4, П5, П6, П7, П8, П9);
			ЭтоПерваяСтрока = Ложь;
			Продолжить;
		КонецЕсли;
		Строка = СтрокаОтступа + Общий.Шаблон(СтрокаШаблона, П1, П2, П3, П4, П5, П6, П7, П8, П9);
		Строки.Добавить(Строка);
	КонецЦикла;
	Возврат ЭтотОбъект;
КонецФункции

Функция Группировка(НовыйУровень) Экспорт
	Уровень = НовыйУровень;
	СтрокаОтступа = СтроковыеФункцииКлиентСервер.СформироватьСтрокуСимволов(Символы.Таб, Уровень);
	Возврат ЭтотОбъект;
КонецФункции	

Функция ГруппировкаНачать() Экспорт
	Группировка(Уровень + 1);
	Возврат ЭтотОбъект;
КонецФункции	

Функция ГруппировкаЗавершить() Экспорт
	Группировка(Уровень - 1);
	Возврат ЭтотОбъект;
КонецФункции	

Функция ПолучитьТекст() Экспорт
	Возврат СтрСоединить(Строки, Символы.ПС);	
КонецФункции

Уровень = 0;
СтрокаОтступа = "";
Строки = Новый Массив;
