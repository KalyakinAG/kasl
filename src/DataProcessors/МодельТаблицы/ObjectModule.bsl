//  Подсистема "Модель таблицы"
//	Автор: Калякин Андрей Г.
//  https://github.com/KalyakinAG/kassl

Перем Таблицы Экспорт;
Перем Колонки;
Перем Колонка;

Функция Таблица(Имя) Экспорт
	Колонки = Новый Массив;
	Таблицы.Вставить(Имя, Новый Структура("ЭтоДерево, Колонки", Ложь, Колонки));
	Колонка = Неопределено;
	Возврат ЭтотОбъект;
КонецФункции

Функция Дерево(Имя) Экспорт
	Колонки = Новый Массив;
	Таблицы.Вставить(Имя, Новый Структура("ЭтоДерево, Колонки", Истина, Колонки));
	Колонка = Неопределено;
	Возврат ЭтотОбъект;
КонецФункции

Функция Колонка(Имя, Заголовок = "") Экспорт
	Колонка = Новый Структура("Имя, Заголовок", Имя, ?(ПустаяСтрока(Заголовок), Имя, Заголовок));
	Колонки.Добавить(Колонка);
	Возврат ЭтотОбъект;
КонецФункции

Функция ТипСтрока(Длина) Экспорт
	Колонка.Вставить("ТипЗначения", ОбщегоНазначения.ОписаниеТипаСтрока(Длина));
	Возврат ЭтотОбъект;
КонецФункции

Функция ТипЧисло(Разрядность, РазрядностьДробнойЧасти = 0, ЗнакЧисла = Неопределено) Экспорт
	Колонка.Вставить("ТипЗначения", ОбщегоНазначения.ОписаниеТипаЧисло(Разрядность, РазрядностьДробнойЧасти, ЗнакЧисла));
	Возврат ЭтотОбъект;
КонецФункции

Функция ТипБулево() Экспорт
	Колонка.Вставить("ТипЗначения", Новый ОписаниеТипов("Булево"));
	Возврат ЭтотОбъект;	
КонецФункции

Функция ТипДата(ЧастиДаты) Экспорт
	Колонка.Вставить("ТипЗначения", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты));
	Возврат ЭтотОбъект;	
КонецФункции

Функция ТипПроизвольный() Экспорт
	Колонка.Вставить("ТипЗначения", Новый ОписаниеТипов);
	Возврат ЭтотОбъект;	
КонецФункции

Функция ТипЗначения(ТипЗначения) Экспорт
	Если ТипЗнч(ТипЗначения) = Тип("Строка") Тогда
		Колонка.Вставить("ТипЗначения", Общий.ТипЗначенияИзСтроки(ТипЗначения));
	Иначе
		Колонка.Вставить("ТипЗначения", ТипЗначения);
	КонецЕсли;
	Возврат ЭтотОбъект;	
КонецФункции

Процедура СоздатьТаблицыВРеквизитахФормы(НовыеРеквизиты = Неопределено) Экспорт
	Перем Колонка;
	Перем ТипЗначения;
	Если НовыеРеквизиты = Неопределено Тогда
		НовыеРеквизиты = Новый Массив;
	КонецЕсли;
	Для Каждого ЭлементТаблицы Из Таблицы Цикл
		Таблица = ЭлементТаблицы.Значение;
		Если Таблица.ЭтоДерево Тогда
			НовыеРеквизиты.Добавить(Новый РеквизитФормы(ЭлементТаблицы.Ключ, Новый ОписаниеТипов("ДеревоЗначений")));
		Иначе
			НовыеРеквизиты.Добавить(Новый РеквизитФормы(ЭлементТаблицы.Ключ, Новый ОписаниеТипов("ТаблицаЗначений")));
		КонецЕсли;
		Для Каждого Колонка Из Таблица.Колонки Цикл
			Если Колонка.Свойство("ТипЗначения", ТипЗначения) Тогда
				НовыеРеквизиты.Добавить(Новый РеквизитФормы(Колонка.Имя, ТипЗначения, ЭлементТаблицы.Ключ, Колонка.Заголовок));
			Иначе
				НовыеРеквизиты.Добавить(Новый РеквизитФормы(Колонка.Имя, Новый ОписаниеТипов("Неопределено"), ЭлементТаблицы.Ключ, Колонка.Заголовок));
			КонецЕсли;						
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

Функция СоздатьТаблицыВСтруктуре(ВходящаяСтруктура = Неопределено) Экспорт
	Перем Таблица;
	Перем Колонки;
	Перем Колонка;
	Перем ТипЗначения;
	ИсходящаяСтруктура = ?(ВходящаяСтруктура = Неопределено, Новый Структура, ВходящаяСтруктура);
	Для Каждого ЭлементТаблицы Из Таблицы Цикл
		ОписаниеТаблицы = ЭлементТаблицы.Значение;
		Если ОписаниеТаблицы.ЭтоДерево Тогда
			Таблица = Новый ДеревоЗначений;
		Иначе
			Таблица = Новый ТаблицаЗначений;
		КонецЕсли;
		Колонки = Таблица.Колонки;
		Для Каждого Колонка Из ОписаниеТаблицы.Колонки Цикл
			Если Колонка.Свойство("ТипЗначения", ТипЗначения) Тогда
				Колонки.Добавить(Колонка.Имя, ТипЗначения);						
			Иначе
				Колонки.Добавить(Колонка.Имя, Новый ОписаниеТипов("Неопределено"));
			КонецЕсли;
		КонецЦикла;
		ИсходящаяСтруктура.Вставить(ЭлементТаблицы.Ключ, Таблица);
	КонецЦикла;
	Возврат ИсходящаяСтруктура;
КонецФункции

Таблицы = Новый Структура;