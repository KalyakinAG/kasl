//@skip-check object-module-export-variable
#Область ОписаниеПеременных

Перем УсловноеОформление Экспорт;
Перем ЭлементОформления;

#КонецОбласти

#Область ПрограммныйИнтерфейс

Функция Установить(УО, Очистить = Истина) Экспорт
	УсловноеОформление = УО;
	Если Очистить Тогда
		УсловноеОформление.Элементы.Очистить();
	КонецЕсли;
	Возврат ЭтотОбъект;
КонецФункции

Функция ЭлементОформления() Экспорт
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	Возврат ЭтотОбъект;
КонецФункции

Функция Элемент(_ИмяЭлемента) Экспорт
	Для Каждого ИмяЭлемента Из ОбщийКлиентСервер.Массив(_ИмяЭлемента) Цикл
		Элемент = ЭлементОформления.Поля.Элементы.Добавить();
		Элемент.Поле = Новый ПолеКомпоновкиДанных(ИмяЭлемента);
		Элемент.Использование = Истина;
	КонецЦикла;
	Возврат ЭтотОбъект;		
КонецФункции

//ЦветФона
//ЦветТекста
//ЦветВДиаграмме
//ЦветГраницы
//СтильГраницы
//Шрифт
//Отступ
//АвтоОтступ
//ГоризонтальноеПоложение
//ВертикальноеПоложение
//Размещение
//ОриентацияТекста
//Формат
//ВыделятьОтрицательные
//ОтметкаНезаполненного
//МинимальнаяШирина
//МаксимальнаяШирина
//РастягиватьПоГоризонтали
//ВесовойКоэффициентШирины
//МинимальнаяВысота
//МаксимальнаяВысота
//Текст
//Видимость
//Доступность
//ТолькоПросмотр
//Отображать
//ОтображатьГрафическоеПредставлениеДанныхВЛегендеДиаграммы
//ОтображатьГрафическоеПредставлениеДанныхВДиаграмме
//ЛинииТрендаВДиаграмме
//ЛинияВДиаграмме
//МаркерВДиаграмме
//ИндикаторВДиаграмме
//ИнформационнаяЛинияВДиаграмме
//ИнформационныйИнтервалВДиаграмме	
Функция Оформление(Параметр, Значение) Экспорт
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра(Параметр, Значение);
	Возврат ЭтотОбъект;
КонецФункции

Функция Отбор(ИмяПоля, ВидСравнения, Значение = Неопределено) Экспорт
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ИмяПоля);
	ЭлементОтбора.ВидСравнения = ВидСравнения;
	ЭлементОтбора.ПравоеЗначение = Значение;
	Возврат ЭтотОбъект;
КонецФункции

#КонецОбласти